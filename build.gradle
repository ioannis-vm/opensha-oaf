/*
 * opensha-oaf dependencies
 */

apply plugin: 'java-library'

ext {
    parentProject = 'opensha-dev'
}

apply from: '../opensha/build-common.gradle'

dependencies {

    /* for OAF */
    api 'org.mongodb:mongodb-driver-sync:4.2.2'
 
    /* no remote repo */
    implementation files('lib/ProductClient.jar')
    api files('lib/wContour-1.6.1.jar')

    implementation project(path: ':opensha')

    testImplementation 'junit:junit:4.12'
}

task appOAFJar(type: Jar) {
    archiveBaseName = 'AftershockGUI-current'
    from { configurations.apiResolvable.collect {
        it.isDirectory() ? it : zipTree(it).matching {
            exclude { it.path.contains('META-INF') }
        }
    }}
    from { configurations.implResolvable.collect {
        it.isDirectory() ? it : zipTree(it).matching {
            exclude { it.path.contains('META-INF') }
        }
    }}
    manifest {
        attributes(
            'Class-Path': configurations.apiResolvable.collect { it.getName() }.join(' '),
            'Main-Class': 'org.opensha.oaf.rj.gui.RJGUITop'
        )
    }
    with jar
}

task appOAF_ETAS_Jar(type: Jar) {
    archiveBaseName = 'AftershockGUI_ETAS-current-'+getDate()
    from { configurations.apiResolvable.collect {
        it.isDirectory() ? it : zipTree(it).matching {
            exclude { it.path.contains('META-INF') }
        }
    }}
    from { configurations.implResolvable.collect {
        it.isDirectory() ? it : zipTree(it).matching {
            exclude { it.path.contains('META-INF') }
        }
    }}
    manifest {
        attributes(
            'Class-Path': configurations.apiResolvable.collect { it.getName() }.join(' '),
            'Main-Class': 'org.opensha.oaf.etas.AftershockStatsGUI_ETAS'
        )
    }
    with jar
}

task oafJar(type: Jar) {
    archiveBaseName = project.name + '-oaf'
    from { configurations.apiResolvable.collect {
        it.isDirectory() ? it : zipTree(it)
    }}
    with jar
}
